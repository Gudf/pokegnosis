script YamakiTown_Sign
  using_msgbox MSGBOX_SIGN
    say "LOCATION_COLOR(YAMAKI TOWN)\n"
    say "{UP_ARROW} LOCATION_COLOR(Yamaki Lake) {RIGHT_ARROW} LOCATION_COLOR(Downey Ranch)$"

script YamakiTown_Fisherman
  lock
    faceplayer
    say "Ugh! Been here all day and not a\n"
    say "single bite!\p"
    say "The book said there were rare\n"
    say "Pokémon in these waters! :($"

script YamakiTown_FatMan
  lock
    faceplayer
    say "Did you know?$", MSGBOX_YESNO
    if it is 1
      say "What? I hadn't even told you yet!$"
    else
      choose_randomly
        option
          say "I heard {COLOR CHARACTER_COLOR}Lord Jinhai{COLOR NO_COLOR} of the {COLOR CHARACTER_COLOR}Elite\n"
          say "Dynasty{COLOR NO_COLOR} built a {COLOR POKEMON_COLOR}robotic Pokémon{COLOR NO_COLOR}!\p"
          say "How insane is that?! I love science!$"
        option
          say "I heard {COLOR CHARACTER_COLOR}Professor Yinxing{COLOR NO_COLOR} is in Axel!\p"
          say "Probably studying all the {COLOR POKEMON_COLOR}rare Pokémon{COLOR NO_COLOR}\n"
          say "you find in the jungle.$"
        option
          say "I heard some rumour about a bunch of\n"
          say "people dressed as ducks holed up in\l"
          say "a temple nearby. What the heck, right?$"
        option
          say "One of my friends told me their friend\n"
          say "told them that he saw a {COLOR CHARACTER_COLOR}mysterious\l"
          say "trainer{COLOR NO_COLOR} with a golden mask in the\l"
          say "streets of {COLOR LOCATION_COLOR}Saikhet City{COLOR NO_COLOR}!$"
        option
          say "Me neither!$"

script YamakiTown_RivalIntro
  lock
    spawncamera
      move camera
        walk_down 2
      move_and_wait rival: 5
        walk_left 6
      move_and_wait player
        walk_down
        face_right
        emote_exclamation_mark
      showportrait PORTRAIT_ZHENG, PORTRAIT_ALARMED
        say "CHARACTER_COLOR({PLAYER})! Hey!\l"
        say "What are you looking like that for?\n"
        say "Don't you know what's going on?$"
      showplayerportrait PORTRAIT_QUESTION
        say "(...)$"
      showportrait PORTRAIT_ZHENG, PORTRAIT_DRUNK_GRIN
        say "You really don't!\l"
        say "Come on! I'll explain on the way!\n"
        say "We're going to LOCATION_COLOR(Downy Ranch)! I'm sure\l"
        say "we'll find it there!$"
      closemessage
      move_and_wait rival: 5
        walk_fast_right 6
      move_and_wait camera
        walk_up 2
      move player
        emote_question_mark
      showplayerportrait PORTRAIT_QUESTION
        say "(...)$"
      addvar VAR_LITTLEROOT_TOWN_STATE, 1

script YamakiTown_HealShrine
  lock
    getpartysize
    when 1
      showplayerportrait PORTRAIT_EYES_CLOSED_GRIN
        say "(Peaceful...)$"
    else
      using_msgbox MSGBOX_YESNO
        say "The sound of the waves fills you\n"
        say "with determination.\p"
        say "Do you want to heal your POKEMON_COLOR(Pokémon)?$"
      when TRUE
        fadescreen 1
        delay 20
        fadescreen 0
        incrementgamestat GAME_STAT_USED_POKECENTER
        special HealPlayerParty
        say "Your POKEMON_COLOR(Pokémon) are looking refreshed!$"